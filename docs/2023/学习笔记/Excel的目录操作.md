# Excel的目录操作

如何建立一个目录

## 1. 建立以Sheet为名的链接

> 注意：这里是我抄袭的，主要是用这个方法来获取其它sheet的名字，为后面的操作做铺垫。
>
> 生成完毕后，复制生成名字的那一列，然后粘贴到记事本，再粘贴回来即可

①点击【公式】 - 定义名称 - 新建名称输入**：shname。**

**②在“引用位置”输入公式，按确定：**

```excel
=MID(GET.WORKBOOK(1),FIND("]",GET.WORKBOOK(1))+1,99)&T(NOW())
```

**公式讲解 ：**

> Get.Workbook（1）是宏表函数，当参数是1时，可以获取当前工作薄中所有工作表名称。
> 由于名称中带有工作簿名称，所以用Find+MID函数截取只含工作表名称的字符串。
> &T(NOW())的作用是让公式自动更新。
> Now函数可以生成自动更新的时间，可以让公式强制刷新
> T函数则可以把数字（时间也是数字）转换为空白。

**③在“主界面”工作表处，输入公式：**

```excel
=IFERROR(HYPERLINK("#"&INDEX(shname,ROW(A1))&"!a1",INDEX(shname,ROW(A1))),"")
```

④启动宏，将公式向下填充，目录就生成啦，最后美化下就好

**公式讲解 ：**

> IFERROR函数用于屏蔽错误。
> HYPERLINK函数用于创建一个快捷方式（跳转）,是EXCEL超级链接的函数实现方法。当单击函数 HYPERLINK 所在的单元格时，Excel将打开链接的文件或跳转到指定的工作表的单元格。
> index函数可以根据位置提取数据，Row函数可以在向下复制时生成1，2，3，4...序数。

**⑤完成后，复制生成名字的那一列，然后粘贴到记事本，再粘贴回来，覆盖原来的那一列。这样我们就得到了存有其它所有Sheet名字的列。**

⑥删除刚刚设置的【名称】，因为这个不是所有电脑都会兼容的。

## 2. 制作跳转链接

> 以下的"B2"单元格是从第一步中保存的Sheet名字列的第一个。

我在"C2"单元格中输入了以下公式，

```excel
=HYPERLINK("#'第一页'!A1","跳转")
```

* `HYPERLINK()`函数的意思是生成一个超链接。
* `第一页`是第一个Sheet的名字。
* `A1`的意思是，导航到其它Sheet的A1单元格。
* `"跳转"`这个是链接的文本，如果改成`B2`(不加双引号)就是B2单元格的内容了。

但我们不能使用硬编码，Sheet名字要是引用`B2`的内容。

改成如下的公式

```excel
=HYPERLINK("#'"&B2&"'!A1","跳转")
```

* `"#'"&B2&"'!A1"`的意思是`"#'第一页'!A1"`
* `"&B2&"`意为：将B2单元格的内容拼接到公式中。
* 改成这样以后，自动链接的功能就完成了。

链接制作完成后，往下拉，后面生成的超链接也会跳转对应的Sheet。

注意：Sheet的名字不要过长，不然会失效。

## 3. 提前其它Sheet的单元格内容

```excel
=INDIRECT(B2&"!B7")
```

意为：引用B2这一Sheet中的B7单元格。

B2是保存Sheet名字单元格的引用，其实应该是下面这样。

```excel
INDIRECT("第一页"&"!B7")
```

## 4. 替换引用内容中的部分文本

```excel
=SUBSTITUTE(INDIRECT("第一页"&"!B7"),"匹配的文本","")
```

`SUBSTITUTE()`函数就是文本替换。

假设其中的`INDIRECT()`函数的内容是*“匹配的文本是一个文本”*，在经过上面的函数替换后，会变成*“是一个文本”*。

## 5. 附录：Excel的日期快捷键

在Excel中，你可以使用"TODAY"函数来生成当前的日期。 

1. 选择你想插入日期的单元格。
2. 输入 =TODAY() 并回车。

请注意，这将只显示日期，而不会显示时间。而且，这个日期是动态的，即每次打开工作表时，它都会更新为当前日期。如果你需要一个静态的日期，那么你应该按 Ctrl + ; 来插入当前日期。

## 6. 附录：Excel的特殊粘贴快捷键

你可以通过复制并粘贴值的方式将一个公式生成的内容变成真的内容。

以下是具体步骤：
1. 选择包含公式的单元格；
2. 按Ctrl+C复制单元格；
3. 然后按Ctrl+Alt+V打开“粘贴特殊”对话框；
4. 在“粘贴特殊”对话框中选择“值”，然后点击确定。

这样，公式生成的内容就变成了真的内容，不再参与公式计算。